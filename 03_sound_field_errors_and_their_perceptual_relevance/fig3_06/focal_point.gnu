#!/usr/bin/gnuplot
# generated by SFS-Toolbox, see: http://github.com/sfstoolbox/sfs
reset
set macros
set loadpath '../../gnuplot'

set t epslatex size 15cm,4.5cm color colortext
set output 'focal_point.tex';

load 'border.cfg'
load 'moreland.pal'
load 'array.cfg'

set format '$%g$'

unset key
set size ratio -1

set xrange [-1.75:1.75]
set yrange [-3.00:0.25]
set cbrange [-1:1]
set tics scale 0.75 out nomirror
set xtics 1 offset 0,0.5
set ytics 1 offset 0.5,0
set xlabel '$x$ / m' offset 0,1
set ylabel '$y$ / m' offset 1.5,0
unset colorbox

set bmargin 3
set tmargin 0

set multiplot layout 1,3
set lmargin 4
set rmargin 0
set label 1 '\ft $10$\,m' at 1.7,-2.8 right front
set label 2 '$\Delta_\text{s}$' at 1,-1 center front
set arrow 1 from -0.043,-1 to 0.043,-1 nohead front lw 3 lc rgb 'white'
set arrow 1 from -0.086,-1 to 0.086,-1 nohead front lw 3 lc rgb 'white'
plot 'focal_point_10m.dat' binary matrix with image,\
     'array_10m.txt' @array_active w l
set border 1
set format y ''
set ytics scale 0
unset ylabel
set lmargin 2
set rmargin 2
set label 1 '\ft $1.8$\,m'
set arrow 1 from -0.096,-1 to 0.096,-1
plot 'focal_point_1.8m.dat' binary matrix with image,\
    'array_1.8m.txt' @array_active w l
set lmargin 0
set rmargin 4
set label 1 '\ft $0.75$\,m'
set arrow 1 from -0.236,-1 to 0.236,-1
plot 'focal_point_0.75m.dat' binary matrix with image,\
    'array_0.75m.txt' @array_active w l
unset multiplot
