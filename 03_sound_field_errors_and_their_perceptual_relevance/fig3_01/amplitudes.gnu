#!/usr/bin/gnuplot
# generated by SFS-Toolbox, see: http://github.com/sfstoolbox/sfs
reset
set macros
set loadpath '../../gnuplot'

set t epslatex size 10cm,5cm color colortext
set output 'amplitudes.tex';

# configuration
load 'border.cfg'
load 'grid.cfg'
load 'paired.pal'
# redefine red line to use dashed line
set style line 6 lt 2 lc rgb '#E31A1C'

unset key
set format '$%g$'
# shaded box
#set object 1 rect from 0,-7 to 1.5,10 back
#set object 1 rect fc rgb '#d6d7d9' fillstyle solid noborder
set xrange [-5.5:0.5]
set yrange [-7:10]
set tics scale 0.75 out nomirror
set xtics 1 offset 0,0.5
set ytics 3 offset 0.5,0
set xlabel '$y$ / m' offset 0,1
set ylabel 'amplitude deviation / dB' offset 1.5,0

set multiplot layout 1,2
set label 1 '\twoD' at -2.5,9.5 center front
set label 2 '\ft\eqref{eq:D_wfs_ps_without_approximation}' \
                                         at -4.5,4.2  center front tc ls 1
set label 3 '\ft\eqref{eq:D_wfs_ps}'     at -4.5,5.7  center front tc ls 2
set label 4 '\ft\eqref{eq:D_wfs_ls}'     at  0.7,-1.1 center front tc ls 4
set label 5 '\ft\eqref{eq:D_wfs_pw}'     at -3.5,-1.2 center front tc ls 6
set label 6 '\ft point source'           at  0.2,5.5  right  front tc ls 2
set label 7 '\ft line source'            at  0.2,4.0  right  front tc ls 4
set label 8 '\ft plane wave'             at  0.2,2.5  right  front tc ls 6

set lmargin 3
set rmargin 1
plot 'amplitudes_2D.txt'  u 1:($11-$2) w l ls 1 lw 3,\
     ''                   u 1:($6-$2)  w l ls 2 lw 3,\
     ''                   u 1:($7-$3)  w l ls 4 lw 3,\
     ''                   u 1:($8-$4)  w l ls 6 lw 3,\
     '<echo "0 0"'                     w p pt 7 ps 1.5 lc rgb 'black'
set border 1
unset ylabel
unset label 6
unset label 7
unset label 8
set ytics scale 0
set format y ''
set label 1 '\twohalfD'
set label 2 '\ft\eqref{eq:D_wfs_ps_25D_without_approximation}' \
                                         at -4.5,0.4 center front tc ls 1
set label 3 '\ft\eqref{eq:D_wfs_ps_25D}' at -4.5,1.9 center front tc ls 2
set label 4 '\ft\eqref{eq:D_wfs_ls_25D}' at  0.9,5   center front tc ls 4
set label 5 '\ft\eqref{eq:D_wfs_pw_25D}' at -3,-3.5  center front tc ls 6
set label 6 '\ft moving listener'        at -6,9.5   center front tc ls 101
set lmargin 2
set rmargin 2
plot 'amplitudes_25D.txt' u 1:($11-$2) w l ls 1 lw 3,\
     ''                   u 1:($6-$2)  w l ls 2 lw 3,\
     ''                   u 1:($7-$3)  w l ls 4 lw 3,\
     ''                   u 1:($8-$4)  w l ls 6 lw 3,\
     '<echo "0 0"'                     w p pt 7 ps 1.5 lc rgb 'black'
unset multiplot
