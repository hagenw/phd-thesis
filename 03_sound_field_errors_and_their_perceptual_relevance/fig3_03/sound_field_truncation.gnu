#!/usr/bin/gnuplot
# generated by SFS-Toolbox, see: http://github.com/sfstoolbox/sfs
reset
set macros
set loadpath '../../gnuplot'

set t epslatex size 15cm,4.5cm color colortext
set output 'sound_field_truncation.tex';

load 'border.cfg'
load 'moreland.pal'
load 'blues.pal'
load 'array.cfg'

set format '$%g$'

unset key
set size ratio -1

set xrange [-2.1:2.1]
set yrange [-3.5:0.5]
set cbrange [0:60]
set tics scale 0.75 out nomirror
set xtics 1 offset 0,0.5
set ytics 1 offset 0.5,0
set cbtics 10 offset -0.5,0
set cbtics add ('$60$\,dB' 60)
set xlabel '$x$ / m' offset 0,1
set ylabel '$y$ / m' offset 1.5,0
unset colorbox

set bmargin 3
set tmargin 0

set multiplot layout 1,3
set lmargin 4
set rmargin 0
plot 'sound_field_inf.dat' binary matrix u 1:2:($3+30) with image,\
     'array_inf.txt' @array_active w l
set lmargin 2
set rmargin 2
set border 1
set format y ''
set ytics scale 0
unset ylabel
#set colorbox
plot 'sound_field_truncated.dat' binary matrix u 1:2:($3+30) with image,\
     'array_truncated.txt' @array_active w l
set colorbox user vertical origin 0.97,0.6 size 0.015,0.4 front
set lmargin 0
set rmargin 4
plot 'sound_field_truncated_round.dat' binary matrix u 1:2:($3+30) with image,\
     'array_truncated_round.txt' @array_active w l
unset multiplot
