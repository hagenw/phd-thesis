#!/usr/bin/gnuplot
# generated by SFS-Toolbox, see: http://github.com/sfstoolbox/sfs
reset
set macros
set loadpath '../../gnuplot'

set t epslatex size 15cm,8.4cm color colortext
set output 'sound_field_time.tex';

load 'border.cfg'
load 'moreland.pal'
load 'array.cfg'

set format '$%g$'

unset key
set size ratio -1


set xrange [-1.75:1.75]
set yrange [-1.75:1.75]
set cbrange [-0.8:0.8] # go only to |0.8| to enhance visibility in figure
set tics scale 0.75 out nomirror
set xtics 1 offset 0,0.5
set ytics 1 offset 0.5,0
set cbtics 1
unset colorbox

set bmargin 2
set tmargin 0


set multiplot layout 2,3
set label 1 '\ft \ac{NFC-HOA}' at -1.7,1.95 left front tc ls 101
load 'yborder.cfg'
set ylabel '$y$ / m' offset 1.5,0
set lmargin 4
set rmargin 0
plot 'nfchoa.dat' binary matrix u 1:2:(55*real($3)) with image,\
     'array.txt' @array_active w l
load 'noborder.cfg'
set lmargin 2
set rmargin 2
plot 'nfchoa_nls64.dat' binary matrix u 1:2:(27*real($3)) with image,\
     'array_nls64.txt' @array_active w p
load 'xborder.cfg'
set xlabel '$x$ / m' offset 0,1
set lmargin 0
set rmargin 4
set label 1 '\ft band-limited \ac{NFC-HOA}'
plot 'nfchoa_nls64_band_limited.dat' binary matrix u 1:2:(4*real($3)) with image,\
     'array_nls64.txt' @array_active w p
load 'xyborder.cfg'
set ylabel '$y$ / m' offset 1.5,0
set lmargin 4
set rmargin 0
set label 1 '\ft \ac{WFS}'
plot 'wfs.dat' binary matrix u 1:2:(real(-$3)) with image,\
     'array.txt' @array_inactive w l,\
     'array_wfs.txt' @array_active w l
load 'xborder.cfg'
set lmargin 2
set rmargin 2
plot 'wfs_nls64.dat' binary matrix u 1:2:(real(-$3)) with image,\
     'array_nls64.txt'  @array_inactive w p,\
     'array_wfs_nls64.txt' @array_active w p
unset multiplot
